buildscript {
	ext {
		springBootVersion = '1.5.1.RELEASE'
		springDependencyManagementVersion = '1.0.1.RELEASE'
	}
	repositories {
		jcenter()
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
		classpath "io.spring.gradle:dependency-management-plugin:${springDependencyManagementVersion}"
	}
}

apply plugin: 'groovy'
apply plugin: "io.spring.dependency-management"
apply plugin: 'org.springframework.boot'

class ProjectVersion {
    Integer major
    Integer minor
    Integer patch
    String  branch

    ProjectVersion( Integer major, Integer minor, Integer patch, String branch ) {
        this.major = major
        this.minor = minor
        this.patch = patch
        this.branch = branch
    }

    @Override
    String toString() {
        String release = '-SNAPSHOT'
        if ( branch == 'master' ) {
            release = 'RELEASE'
        }
        else if ( branch == 'development' ) {
            release = 'MILESTONE'
        }
        "${major}.${minor}.${patch}.${release}"
    }
}

version = new ProjectVersion( major.toInteger(), minor.toInteger(), patch.toInteger(), branch.toString() )

sourceCompatibility = 1.8

repositories {
	jcenter()
}

defaultTasks = ['clean', 'build']

dependencies {
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.codehaus.groovy:groovy')

        // Amazon X-Ray Stuff
        compile("com.amazonaws:aws-java-sdk-dynamodb")
        compile("com.amazonaws:aws-xray-recorder-sdk-core")
        compile("com.amazonaws:aws-xray-recorder-sdk-aws-sdk")
        compile("com.amazonaws:aws-xray-recorder-sdk-aws-sdk-instrumentor")
        compile("com.amazonaws:aws-xray-recorder-sdk-apache-http")
        compile("com.amazonaws:aws-xray-recorder-sdk-sql-postgres")
        compile("com.amazonaws:aws-xray-recorder-sdk-sql-mysql")
}

dependencyManagement {
	imports {
		mavenBom 'io.spring.platform:platform-bom:Brussels-RELEASE'

                mavenBom 'com.amazonaws:aws-java-sdk-bom:1.11.39'
                mavenBom 'com.amazonaws:aws-xray-recorder-sdk-bom:1.0.5-beta'
	}
}
